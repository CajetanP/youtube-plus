apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

ext.versionMajor = 0
ext.versionMinor = 9
ext.versionPatch = 2
ext.versionClassifier = null
ext.isSnapshot = true
ext.minimumSdkVersion = 19

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.cajetan.youtubeplus"
        targetSdkVersion 28
        minSdkVersion 19
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "YTPlus-" + generateVersionName())
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName;
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.11"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.0.1'

    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha09"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha09"
    implementation 'androidx.preference:preference:1.1.0-alpha02'

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.android.support:design:28.0.0'

    implementation 'pub.devrel:easypermissions:2.0.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "org.jetbrains.anko:anko-commons:0.10.8"

    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.1.0-alpha01"
    implementation "androidx.room:room-runtime:2.1.0-alpha03"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    annotationProcessor "androidx.room:room-compiler:2.1.0-alpha03"
    kapt "androidx.room:room-compiler:2.1.0-alpha03"

    implementation('com.google.api-client:google-api-client-android:1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation('com.google.apis:google-api-services-youtube:v3-rev206-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation project(":youtube-player")

    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }
}

repositories {
    mavenCentral()
    google()
}
