apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 6
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = true
ext.minimumSdkVersion = 19

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.cajetan.youtubeplus"
        targetSdkVersion 26
        minSdkVersion 19
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "YTPlus-" + generateVersionName())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName;
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'pub.devrel:easypermissions:0.3.0'
    implementation 'com.google.android.gms:play-services-auth:10.2.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.android.support:design:26.1.0'


    implementation ('com.google.api-client:google-api-client-android:1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation ('com.google.apis:google-api-services-youtube:v3-rev206-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation 'com.android.support:recyclerview-v7:26.1.0'

    implementation project(":youtube-player")

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
